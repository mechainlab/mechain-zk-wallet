
import "hashes/sha256/1024bit.code" as sha256of1024
import "../common/concat/concatenate256x128x256x256.zok" as concatenate256x128x256x256
import "utils/pack/unpack128.code" as unpack128
import "utils/pack/nonStrictUnpack256.code" as unpack256
import "../common/packing/unpack2x128To256.zok" as unpack2x128To256
import "../common/hashes/sha256/padding/shaPad896To1024" as shaPad896To1024

// Inputs for main are:
// - value (public) is the 128 bit coin value
// - publicKey (private) is the public key of the commitment derived by hashing the Secret Key Sk of the commitment. IT IS KEPT PRIVATE FOR ZK!!!
// - salt (private) is the salt for the commitment
// - commitment (public) is the commitment

def main(field publicInputHash, private field contractAddress, private field value, private field[2]  publicKey, private field[2] salt, private field[2] commitment)->():

	// Unpack the inputs of main() to 128 bits. We'll unpack each field to its own 128 bit string for simplicity for now. The unpacked binary string is in big endian format, left-padded with 0's. (NOTE: THIS METHOD OF PADDING IS DIFFERENT FROM THE PADDING REQUIRED FOR SHA256 - i.e. unpack128() padding is very different from that produced by shaPad64To512())
	// unpack128 unpacks a field element to 128 field elements.
	// The coin value is only 128 bits - no one will want more money than that.
	0 == if value < 2**128 then 0 else 1 fi //...but lets check just in case

	field[256] publicInputHashBits = unpack256(publicInputHash)
	field[256] contractAddressBits = unpack256(contractAddress)
	field[128] valueBits = unpack128(value)
	field[256] saltBits = unpack2x128To256(salt)
	field[256] publicKeyBits = unpack2x128To256(publicKey)
	field[256] commitmentBits = unpack2x128To256(commitment)

	// Compute commitment = H(value|publicKey|salt):
	field[896] commitmentPreImage = concatenate256x128x256x256(contractAddressBits, valueBits, publicKeyBits, saltBits)
	field[1024] preimage1024 = shaPad896To1024(commitmentPreImage)
	field[256] commitmentBitsCheck = sha256of1024(preimage1024[0..256], preimage1024[256..512], preimage1024[512..768], preimage1024[768..1024])

	// This checks if the commitmentBitsCheck we computed using the 'value' (which is hash(value|publicKey|salt)) is equal to the input commitment:
	commitmentBits == commitmentBitsCheck

	// Check that the 'public inputs' hash to the publicInputHash:
	field[896] preimage896 = concatenate256x128x256x256(contractAddressBits, valueBits, commitmentBits, publicKeyBits)
	preimage1024 = shaPad896To1024(preimage896)
	field[256] publicInputHashBitsCheck = sha256of1024(preimage1024[0..256], preimage1024[256..512], preimage1024[512..768], preimage1024[768..1024])

	publicInputHashBits[8..256] == publicInputHashBitsCheck[8..256]


	return
