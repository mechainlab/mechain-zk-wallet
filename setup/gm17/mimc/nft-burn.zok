import "hashes/sha256/1536bit.code" as sha256of1536
import "hashes/sha256/1024bit.code" as sha256of1024
import "hashes/sha256/512bit.code" as sha256of512
import "hashes/sha256/IVconstants.code" as IVconstants
import "hashes/sha256/shaRoundNoBoolCheck.code" as sha256

import "../common/concat/concatenate256x256.zok" as concatenate256x256
import "../common/concat/concatenate3x256.zok" as concatenate3x256
import "../common/concat/concatenate4x256.zok" as concatenate4x256
import "../common/concat/concatenate5x256.zok" as concatenate5x256

import "utils/pack/unpack128.code" as unpack128
import "utils/pack/nonStrictUnpack256.code" as unpack256
import "utils/pack/pack128.code" as pack128
import "utils/pack/pack256.code" as pack256
import "../common/packing/unpack2x128To256.zok" as unpack2x128To256

import "../common/hashes/sha256/padding/shaPad256To512.zok" as shaPad256To512
import "../common/hashes/sha256/padding/shaPad432To512.zok" as shaPad432To512
import "../common/hashes/sha256/padding/shaPad512To1024" as shaPad512To1024
import "../common/hashes/sha256/padding/shaPad768To1024" as shaPad768To1024
import "../common/hashes/sha256/padding/shaPad1024To1536" as shaPad1024To1536
import "../common/hashes/sha256/padding/shaPad1280To1536" as shaPad1280To1536

import "../common/merkle-tree/mimc-root.zok" as mimcroot

// Inputs for main are:

// payTo - the public Ethereum address to which the burned tokens get paid
// asset - the tokenId of the ERC-721 token
// secretKey - the secret key of the spender
// salt - the salt for the commitment being spent
// siblingPath - the sibling-path from the commitment (being spent) to the root of the merkle tree
// nullifier - the nullifier for the commitment being spent
// root - the merkle root

// publicInputHash = hash(root, nullifier, asset, payTo)
// Each field element passed to main() can be at most 128bits, to make use of unpack128() so most variables occupy two fields (except the publicInputHash, which we maximize as 248-bits)

def main(field publicInputHash,  private field contractAddress, private field[2] payTo, private field[2] asset, private field[2] secretKey, private field[2] salt, private field[32] path, private field order, private field[2] nullifier, private field root)->():

	//Unpack the inputs of main() to 128 bits. We'll unpack each field to its own 128 bit string for simplicity for now. Later efficiencies could be made by grouping some inputs.

	field[256] publicInputHashBits = unpack256(publicInputHash)
	field[256] contractAddressBits = unpack256(contractAddress)
	field[256] payToBits = unpack2x128To256(payTo)
	field[256] assetBits = unpack2x128To256(asset)
	field[256] secretKeyBits = unpack2x128To256(secretKey)
	field[256] saltBits = unpack2x128To256(salt)
	field[256] nullifierBits = unpack2x128To256(nullifier)

	field[256] rootBits = unpack256(root)


	// First we construct a proof that the nullifier is correct.
	// Concatenate salt|secretKey.

	field[512] preimage512 = concatenate256x256(saltBits, secretKeyBits)
	field[1024] nullifierPadded = shaPad512To1024(preimage512)
	field[256] nullifierBitsCheck = sha256of1024(nullifierPadded[0..256], nullifierPadded[256..512], nullifierPadded[512..768], nullifierPadded[768..1024])


	// Prove that the commitment is in the Merkle tree

	// First, calculate Alice's public key from her private key: publicKey = H(secretKey).
	field[512] secretKeyPadded = shaPad256To512(secretKeyBits)
	field[256] publicKeyBits = sha256of512(secretKeyPadded[0..256], secretKeyPadded[256..512])


	// Next calculate the commitment = H(asset|publicKey|salt)

	field[1024] commitmentPreImage = concatenate4x256(contractAddressBits, assetBits, publicKeyBits, saltBits)
	field[1536] commitmentPadded = shaPad1024To1536(commitmentPreImage)
	field[256] shaHash = sha256of1536(commitmentPadded[0..256], commitmentPadded[256..512], commitmentPadded[512..768], commitmentPadded[768..1024], commitmentPadded[1024..1280], commitmentPadded[1280..1536]) // commitment

	// concatenate siblings and hash all the way up to the root:
	field mimcHash = pack256(shaHash)
	mimcHash = mimcroot(path, order, mimcHash)

	// save the computed root:
	field rootCheck = mimcHash

	// Now test the proofs

	nullifierBits == nullifierBitsCheck
	root == rootCheck


	// Check that the 'public inputs' hash to the publicInputHash:
	// (This also intertwines the 'payTo' input into the proof)

	field[1280] preimage1280 = concatenate5x256(contractAddressBits, rootBits, nullifierBits, assetBits, payToBits)

	field[1536] publicInputPadded = shaPad1280To1536(preimage1280)
	field[256] publicInputHashBitsCheck = sha256of1536(publicInputPadded[0..256], publicInputPadded[256..512], publicInputPadded[512..768], publicInputPadded[768..1024], publicInputPadded[1024..1280], publicInputPadded[1280..1536])

	publicInputHashBits[8..256] == publicInputHashBitsCheck[8..256]


	return
